buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.1.16'
        classpath 'org.gretty:gretty:3.0.7'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: 'checkstyle'
    version = '1.0'
    ext {
        appName = "piazzaPanic"
        gdxVersion = '1.11.0'
        roboVMVersion = '2.3.16'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'
        checkStyleVersion = '10.4'
    }

    configurations {
        checkstyleRules
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        checkstyleRules("com.puppycrawl.tools:checkstyle:${checkStyleVersion}") {
            transitive = false
        }
        checkstyle "com.puppycrawl.tools:checkstyle:${checkStyleVersion}"
    }

    checkstyle {
        toolVersion "${checkStyleVersion}"
        config = resources.text.fromArchiveEntry(configurations.checkstyleRules, 'google_checks.xml')
    }



}

project(":desktop") {
    apply plugin: "java-library"

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        
    }
}

project(":core") {
    apply plugin: "java-library"

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        api "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
        api "com.badlogicgames.ashley:ashley:$ashleyVersion"
        api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":tests") {
    apply plugin: "java"
    apply plugin: 'jacoco'
    // Add all the code packages to the jacoco report.
    gradle.projectsEvaluated {
        jacocoTestReport {
            def projects = configurations.implementation.getAllDependencies().withType(ProjectDependency).collect{it.getDependencyProject()}
            projects.each {
                additionalSourceDirs files(it.sourceSets.main.java.srcDirs)
                additionalClassDirs files(it.sourceSets.main.output)
            }
        }
    }
    sourceSets.test.java.srcDirs = ["src/"]

    dependencies {

        implementation project(":core")
        implementation "junit:junit:4.+"
        implementation "org.mockito:mockito-core:3.+"

        implementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
        implementation "com.badlogicgames.ashley:ashley:$ashleyVersion"
        implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"
    }
}
